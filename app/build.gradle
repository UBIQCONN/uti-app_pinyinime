apply plugin: 'com.android.application'

def getVersionTag = { variant ->
    def stdout
    try {
        stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "describe", "--match", "${variant}/*", "--tags"
            standardOutput = stdout
        }
        def longTag = stdout.toString().trim()

        return longTag.substring(longTag.indexOf('/') + 1)
    } catch (exception) {
        return "0.0.0";
    }
}

def getVersionCode = { tag ->
    def parts = tag.tokenize('.')
    if(parts.size() != 3 || parts[1].length() > 2 || parts[2].length() > 2) {
        throw new RuntimeException("Invalid version tag \"${tag}\", format \"x.y.z\" expected.")
    }
    if(parts[1].length() == 1) {
        parts[1] = "0" + parts[1]
    }
    if(parts[2].length() == 1) {
        parts[2] = "0" + parts[2]
    }
    return Integer.parseInt(parts[0] + parts[1] + parts[2])
}

def getVersionName = { tag ->
    return tag
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.android.inputmethod.pinyin"
        minSdkVersion 26
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 29
        versionCode 10000
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
        aaptOptions {
            noCompress "dat"
        }
        archivesBaseName = "ime_pinyin"
    }
    signingConfigs {
        sign_aosp {
            keyAlias 'aosp'
            keyPassword 'ubiqconn'
            storeFile file('../signkey/aosp_platform.jks')
            storePassword 'ubiqconn'
        }
        sign_nxp_evk {
            keyAlias 'nxp_evk'
            keyPassword 'ubiqconn'
            storeFile file('../signkey/nxp_evk.jks')
            storePassword 'ubiqconn'
        }
        sign_ubiqconn {
            keyAlias 'ubiqconn'
            keyPassword 'ubiqconn'
            storeFile file('../signkey/ubiqconn_platform.jks')
            storePassword 'ubiqconn'
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.sign_nxp_evk
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.sign_nxp_evk
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }
    namespace "com.android.inputmethod.pinyin"

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def versionTag = getVersionTag('release')
            def myVersionCode = getVersionCode(versionTag)
            def myVersionName = getVersionName(versionTag)

            println variant.name + "[" + myVersionCode + ", " + myVersionName + "]"

            if (versionTag.equalsIgnoreCase("0.0.0")) {
                output.versionNameOverride = defaultConfig.versionName
                output.versionCodeOverride = defaultConfig.versionCode
            } else {
                output.versionNameOverride = myVersionName
                output.versionCodeOverride = myVersionCode
            }
            outputFileName = "${archivesBaseName}-${variant.name}-${output.versionNameOverride}.apk"
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

// Define a custom task to copy the APK
//task copyApk(type: Copy) {
//    def apkDir = file("${project.buildDir}/outputs/apk/release")
//    from apkDir
//    include "${archivesBaseName}-release*.apk" // Replace with the actual APK filename if needed
//    into project.rootDir
//}

task copyApk(type: Copy) {
    // Iterate over application variants
    android.applicationVariants.all { variant ->
        // Get the output file for the variant
        def outputFileName = variant.outputs.first().outputFileName

        // Construct the source file path
        def sourceFile = "${project.buildDir}/outputs/apk/${variant.name}/${outputFileName}"

        // Set the destination directory
        def destinationDirectory = "${project.rootDir}/"

        //println "source file " + sourceFile
        // Copy the APK file
        from sourceFile
        into destinationDirectory
        rename { fileName ->
            // Rename the file if needed
            fileName
        }

        // Print a debug message
        println "Copying $outputFileName to $destinationDirectory"
    }
}

task delApk(type: Delete) {
    def apkFile = file("${project.rootDir}/*.apk") // Replace with the actual APK filename if needed
    delete apkFile
}
// Make the copyApk task depend on the assembleRelease task
tasks.getByName("assemble").dependsOn(copyApk)
tasks.getByName("clean").dependsOn(delApk)
